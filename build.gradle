group = "$groupPackage" as Object
version = "$projectVersion" as Object
description = "$rootProject.name"

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.github.ben-manes:gradle-versions-plugin:$gradlePluginVersionsVersion"
        classpath "gradle.plugin.com.hierynomus.gradle.plugins:license-gradle-plugin:$licenseGradlePluginVersion"
        classpath "org.ajoberstar.grgit:grgit-gradle:$grgitGradlePluginVersion"
        classpath "com.diffplug.spotless:spotless-plugin-gradle:$spotlessGradlePluginVersion"
    }
}

apply plugin: "java"
apply plugin: "java-library"
apply plugin: "maven-publish"
apply plugin: "signing"
apply plugin: "com.github.ben-manes.versions"
apply plugin: "com.github.hierynomus.license"
apply plugin: "jacoco"
apply plugin: "org.ajoberstar.grgit"
apply plugin: "com.diffplug.spotless"

spotless {

    format "misc", {
        // define the files to apply `misc` to
        target "*.gradle", "*.md", ".gitignore"

        // define the steps to apply to those files
        trimTrailingWhitespace()
        indentWithSpaces() // or spaces. Takes an integer argument if you don"t like 4
        endWithNewline()
    }
    java {
        eclipse("4.19.0").configFile("src/main/resources/alpharo-formatter.xml")
    }
}

task tagRelease {
    description = "Tags the current head with the project\'s version."
    doLast {
        grgit.tag.add {
            name = "RELEASE-${project.version.toString()}"
            message = "New release in version ${version}"
        }
    }
}

sourceCompatibility = "$projectSourceCompatibility" as Object

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        name "Sonatype Nexus Releases"
        url "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
        mavenContent {
            releasesOnly()
        }
    }
    maven {
        name "Sonatype Nexus Snapshots"
        url "https://oss.sonatype.org/content/repositories/snapshots"
        mavenContent {
            snapshotsOnly()
        }
    }
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
    // uncomment the next line for build with compiler argument -Xlint:deprecation
    // options.compilerArgs << "-Xlint:deprecation"
    // uncomment the next line for build with compiler argument -Xlint:unchecked
    // options.compilerArgs << "-Xlint:unchecked"
}

task sourcesJar(type: Jar) {
    archiveClassifier.set("sources")
    from(sourceSets.main.allJava)
}

task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier.set("javadoc")
    javadoc.properties.failOnError = true
}

javadoc {
    failOnError = false
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption("html5", true)
    }
}

def releaseVersion = !version.endsWith("SNAPSHOT")

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = "$rootProject.name"
            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom {
                name = "$rootProject.name"
                description = "$projectDescription"
                url = "$projectScmProviderUrl"+"$projectHolderUsername"+"$slash"+"$rootProject.name"
                organization {
                    name = "$projectOrganizationName"
                    url = "$projectOrganizationUrl"
                }
                issueManagement {
                    system = "$projectIssueManagementSystem"
                    url = "$projectScmProviderUrl"+"$projectHolderUsername"+"$slash"+"$rootProject.name"+"$issuesPath"
                }
                licenses {
                    license {
                        name = "$projectLicenseName"
                        url = "$projectLicenseUrl"
                        distribution = "$projectLicenseDistribution"
                    }
                }
                developers {
                    developer {
                        id = "$projectHolderUsername"
                        name = "$projectLeaderName"
                    }
                }
                scm {
                    connection = "$projectScmGitUrlPrefix" + "$projectScmProviderDomain" + "$colon" + "$projectHolderUsername" + "$slash" + "$rootProject.name" + "$projectScmGitUrlSuffix"
                    developerConnection = "$projectScmGitUrlPrefix" + "$projectScmProviderDomain" + "$colon" + "$projectHolderUsername" + "$slash" + "$rootProject.name" + "$projectScmGitUrlSuffix"
                    url = "$projectScmGitUrlPrefix" + "$projectScmProviderDomain" + "$colon" + "$projectHolderUsername" + "$slash" + "$rootProject.name" + "$projectScmGitUrlSuffix"
                }
            }
        }
    }
    repositories {
        maven {
            credentials {
                username System.getenv("$projectRepositoriesUserNameKey") ?: project.property("$projectRepositoriesUserNameKey")
                password System.getenv("$projectRepositoriesPasswordKey") ?: project.property("$projectRepositoriesPasswordKey")
            }
            def releasesRepoUrl = "$projectRepositoriesReleasesRepoUrl"
            def snapshotsRepoUrl = "$projectRepositoriesSnapshotsRepoUrl"
            url = releaseVersion ? releasesRepoUrl : snapshotsRepoUrl
        }
    }
}

signing {
    if (releaseVersion) {
        sign publishing.publications.mavenJava
    }
}

jar {
    manifest {
        attributes(
                "Name"                  : project.name,
                "Manifest-Version"      : project.version,
                "Implementation-Title"  : "$groupPackage" + "." + "$rootProject.name",
                "Implementation-Version": project.version,
                "Implementation-Vendor" : "$projectLeaderName",
                "Created-By"            : "Gradle ${gradle.gradleVersion}",
                "Built-By"              : "$projectLeaderName",
                "Build-Timestamp"       : new java.text.SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZ").format(new Date()),
                "Build-Jdk"             : "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
                "Build-OS"              : "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}")
    }
}

license {
    ext.year="$projectInceptionYear"
    ext.owner="$projectLeaderName"
    header rootProject.file("src/main/resources/LICENSE.txt")
    excludes(["**/README",
              "**/README.md",
              "**/LICENSE",
              "**/NOTICE",
              "**/*.xml",
              "**/*.xsl",
              "**/*.xsd",
              "**/*.dtd",
              "**/*.html",
              "**/*.jsp",
              "**/*.jpa",
              "**/*.sql",
              "**/*.properties",
              "**/*.bat",
              "**/*.gradle",
              "**/*.MF",
              "**/*.txt",
              "**/*.vm",
              "**/*.log",
              "**/*.map",
              "**/*.js.map",
              "**/*.tmpl",
              "**/*.js.tmpl",
              "**/*.editorconfig",
              "src/test/resources/**",
              "src/main/resources/**",
              "out/**",
              "build/**"])
}
// workhack for license issue #76
gradle.startParameter.excludedTaskNames += "licenseMain"
gradle.startParameter.excludedTaskNames += "licenseTest"

test {
    useTestNG()
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled true
    }
}

check.dependsOn jacocoTestReport

dependencies {
    implementation("commons-codec:commons-codec:$commonsCodecVersion")
    implementation("commons-io:commons-io:$commonsIoVersion")
    implementation("org.apache.commons:commons-lang3:$commonsLang3Version")
    implementation("com.google.guava:guava:$guavaVersion")
    implementation("io.github.astrapi69:crypt-api:$cryptApiVersion")
    implementation("io.github.astrapi69:crypt-data:$cryptDataVersion")
    implementation("io.github.astrapi69:throw-able:$throwAbleVersion")
    implementation("io.github.astrapi69:randomizer-core:$randomizerVersion")
    implementation("io.github.astrapi69:file-worker:$fileWorkerVersion")
    implementation("io.github.astrapi69:silly-collections:$sillyCollectionsVersion")
    implementation("io.github.astrapi69:jobj-core:$jobjCoreVersion")
    implementation("io.github.astrapi69:silly-beans:$sillyBeansVersion")
    implementation("io.github.astrapi69:jobj-clone:$jobjCloneVersion")
    implementation("org.bouncycastle:bcprov-jdk15on:$bouncycastleVersion")
    implementation("org.bouncycastle:bcprov-ext-jdk15on:$bouncycastleVersion")
    implementation("org.bouncycastle:bcpkix-jdk15on:$bouncycastleVersion")
    testImplementation("io.github.astrapi69:silly-strings:$sillyStringsVersion")
    testImplementation("io.github.astrapi69:json-extensions:$jsonExtensionsVersion")
    testImplementation("io.github.astrapi69:checksum-up:$checksumUpVersion")
    testImplementation("io.github.astrapi69:test-objects:$testObjectsVersion")
    testImplementation("io.github.astrapi69:silly-io:$sillyIoVersion")
    testImplementation("io.github.benas:random-beans:$randomBeansVersion")
    testImplementation("org.meanbean:meanbean:$meanbeanVersion")
    testImplementation("org.testng:testng:$testngVersion")
}
